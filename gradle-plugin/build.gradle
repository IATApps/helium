plugins {
  id 'groovy'
  id 'maven'
  id 'signing'
  id 'checkstyle'
  id 'codenarc'
  id 'com.github.johnrengelman.shadow' version '1.2.0' // shadowing dependencies
}

dependencies {
  compile localGroovy()
  compile gradleApi()

  compile project(':helium')
  compile 'com.android.tools.build:gradle:1.0.0'

  testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
    exclude group: 'org.codehaus.groovy'
  }

  compile 'com.squareup:javawriter:2.5.0'

}

afterEvaluate {
  uploadArchives {
    repositories {
      mavenDeployer {
        pom.project {
          name 'Helium Gradle plugin'
        }
        pom.whenConfigured { def pom ->
          pom.dependencies.each {
            if (it.groupId == 'com.android.tools.build') {
              it.scope = 'provided'
            }
          }
        }
      }
    }
  }
}

shadowJar {
  ['com.squareup.javawriter', 'com.google'].each {
    def newPackage = "com.stanfy.helium.shadow.${it}"
    relocate it, newPackage
  }
  dependencies {
    include dependency('com.squareup:javawriter:.*')
    include dependency('com.google.common:.*:.*')
    include dependency('com.google.gson:.*:.*')
  }
}
